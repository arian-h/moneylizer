buildscript {
	repositories {
		mavenCentral()
	}
}

plugins {
	id 'com.moowork.node' version '1.2.0'
	id 'java'
	id 'eclipse'
	//a plugin to run the gradle application using command line: ./gradlew run
    id 'application'
	id 'org.springframework.boot' version '2.0.4.RELEASE'
}

group = 'com.karen'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

//used by the application plugin
mainClassName = 'com.karen.moneylizer.MoneylizerApplication' 

repositories {
	mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': 'com.karen.moneylizer.MoneylizerApplication'
    }
}

node {
    version = '8.9.1'
	//If true, it will download node using above parameters.
	//If false, it will try to use globally installed node.
    download = true
    //the work directory for npm
    npmWorkDir = file('src/main/ui')
    //the directory for node_modules
    nodeModulesDir = file('src/main/ui')
}

task webpack(type: Exec, dependsOn: 'npmInstall') {
	workingDir 'src/main/ui'
	commandLine 'node_modules/webpack/bin/webpack.js'
	//to monitor the changes and 
	//decide whether to use cached result for the next run or not
	inputs.file('package-lock.json')
	inputs.dir('src')
}

task copyTask(type: Copy, dependsOn: 'webpack') {
    from 'src/main/ui/bundle.js'
    into 'src/main/resources/public/static/js/'
}

processResources.dependsOn 'copyTask'

dependencies {
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.4.RELEASE'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.0.4.RELEASE'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.0.4.RELEASE'
    compile group: 'mysql', name: 'mysql-connector-java', version:'5.1.44'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'com.google.guava', name: 'guava', version: '26.0-jre'
    compile group: 'org.springframework.security', name: 'spring-security-core', version: '5.0.7.RELEASE'
    compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    compile group: 'org.springframework.security', name: 'spring-security-config', version: '5.0.7.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-web', version: '5.0.7.RELEASE'
}
